apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.rubyhuntersky.peregrine"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        warning 'InvalidPackage'
    }
    productFlavors {
        sandbox {
            applicationIdSuffix ".sandbox"
            buildConfigField 'String', 'ETRADE_API_KEY', project.hasProperty('EtradeSandboxKeyProp') ? "\"${EtradeSandboxKeyProp}\"" : '"Define Etrade Sandbox key"'
            buildConfigField 'String', 'ETRADE_API_SECRET', project.hasProperty('EtradeSandboxSecretProp') ? "\"${EtradeSandboxSecretProp}\"" : '"Define Etrade Sandbox secret"'
        }
        production {
            buildConfigField 'String', 'ETRADE_API_KEY', project.hasProperty('EtradeProductionKeyProp') ? "\"${EtradeProductionKeyProp}\"" : '"Define Etrade Production key"'
            buildConfigField 'String', 'ETRADE_API_SECRET', project.hasProperty('EtradeProductionSecretProp') ? "\"${EtradeProductionSecretProp}\"" : '"Define Etrade Production secret"'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'io.reactivex:rxjava:1.0.15'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.squareup.okhttp3:okhttp:3.0.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.0.1'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile('com.github.wehjin:GxAndroid:3333eb16c42e984839ae65263e28d50e0243fcb2') {
        transitive = true
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
repositories {
    mavenCentral()
}
